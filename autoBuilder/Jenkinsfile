pipeline {
    agent any
    environment {
        USERNAME = 'bella2391'
        REPO_NAME = 'Jenkin-Dependency-Provider'
        BRANCH_NAME = 'master'
        REPO_URL = "https://github.com/${USERNAME}/${REPO_NAME}.git"
        JAR_PATH = 'build/libs/FMC-Dependency-1.0.0.jar'
        TAG_NAME = "${REPO_NAME}-${BUILD_NUMBER}"
        // Caution: Do not set this value too low, as it may cause Jenkins to get stuck in a loop of aborted builds.
        // build intervals (mininutes)
        BUILD_INTERVAL_MINUTES = 5
    }
    stages {
        stage('Check Build Interval') {
            steps {
                script {
                    def previousBuild = currentBuild.previousBuild
                    if (previousBuild != null) {
                        def currentTime = System.currentTimeMillis()
                        def lastBuildTime = previousBuild.getTimeInMillis()
                        def diffMinutes = (currentTime - lastBuildTime) / (1000 * 60)
                        def diffMinutesNum = diffMinutes.toDouble()
                        def intervalNum = BUILD_INTERVAL_MINUTES.toDouble()
                        echo "Condition result: ${diffMinutesNum < intervalNum}"
                        
                        if (diffMinutesNum < intervalNum) {
                            currentBuild.result = 'ABORTED'
                            error("Build aborted: Only ${diffMinutes} minutes since last build. Must wait at least ${BUILD_INTERVAL_MINUTES} minutes.")
                        }
                    }
                }
            }
        }
        stage('Checkout') {
            steps {
                git branch: "${BRANCH_NAME}",
                url: "${REPO_URL}"
            }
        }
        stage('Build') {
            steps {
                sh './gradlew build'
            }
        }
        stage('Test') {
            steps {
                catchError(buildResult: 'SUCCESS', stageResult: 'FAILURE') {
                    sh './gradlew test'
                }
            }
        }
        stage('Configure Git') {
            steps {
                withCredentials([
                    string(credentialsId: 'GitUserName', variable: 'GIT_USER_NAME'),
                    string(credentialsId: 'GitUserEmail', variable: 'GIT_USER_EMAIL')
                ]) {
                    sh '''
                    git config --global user.name "${GIT_USER_NAME}"
                    git config --global user.email "${GIT_USER_EMAIL}"
                    '''
                }
            }
        }
        stage('Tag and Push') {
            steps {
                script {
                    withCredentials([string(credentialsId: 'GitHub-Token', variable: 'GIT_TOKEN')]) {
                        sh '''
                        if [ -d "${REPO_NAME}" ]; then
                            rm -rf ${REPO_NAME}
                        fi
                        if git rev-parse "refs/tags/${TAG_NAME}" >/dev/null 2>&1; then
                            echo "Tag ${TAG_NAME} already exists. Skipping tag creation."
                        else
                            git tag -a "${TAG_NAME}" -m "Jenkins Build #${BUILD_NUMBER}"
                            git push "https://oauth2:${GIT_TOKEN}@github.com/${USERNAME}/${REPO_NAME}.git" --tags
                        fi
                        '''
                    }
                }
            }
        }
        stage('Create Release on GitHub') {
            steps {
                script {
                    withCredentials([string(credentialsId: 'GitHub-Token', variable: 'GIT_TOKEN')]) {
                        def response = httpRequest(
                            acceptType: 'APPLICATION_JSON',
                            contentType: 'APPLICATION_JSON',
                            httpMode: 'POST',
                            url: "https://api.github.com/repos/${USERNAME}/${REPO_NAME}/releases",
                            requestBody: """{
                                "tag_name": "${TAG_NAME}",
                                "name": "Release ${BUILD_NUMBER}",
                                "body": "Release description",
                                "draft": false,
                                "prerelease": false
                            }""",
                            customHeaders: [[name: 'Authorization', value: "token ${GIT_TOKEN}"]]
                        )
                        
                        echo "GitHub Release response content: ${response.content}"
                        
                        def releaseId = new groovy.json.JsonSlurper().parseText(response.content).id
                        echo "GitHub Release ID: ${releaseId}"

                        def jarFilePath = "${JAR_PATH}"
                        if (!fileExists(jarFilePath)) {
                            error "JAR file not found: ${jarFilePath}"
                        }
                        def jarFileBytes = readFile(file: jarFilePath, encoding: 'ISO-8859-1').getBytes('ISO-8859-1')
                        def uploadUrl = "https://uploads.github.com/repos/${USERNAME}/${REPO_NAME}/releases/${releaseId}/assets?name=${jarFilePath.split('/').last()}"

                        def uploadResponse = httpRequest(
                            acceptType: 'APPLICATION_JSON',
                            contentType: 'APPLICATION_OCTETSTREAM',
                            httpMode: 'POST',
                            url: uploadUrl,
                            requestBody: new String(jarFileBytes, 'ISO-8859-1'),
                            customHeaders: [[name: 'Authorization', value: "token ${GIT_TOKEN}"],
                                            [name: 'Content-Type', value: 'application/java-archive']]
                        )
                        echo "GitHub Upload Response: ${uploadResponse}"
                    }
                }
            }
        }
    }
}